#!/usr/bin/perl -w

=head1 NAME

mkextrepo

=head1 SYNOPSIS

    mkextrepo [options] dist_version

Options:

    -h,--help		Display brief help message
    -v,--verbose	Increase verbosity
    -q,--quiet		Decrease verbosity
    -r,--root=DIR	Specify external distribution root directory
    -n,--name=NAME	Specify external distribution name
    -f,--force		Force overwriting of existing media.cfg files

=cut

use Getopt::Long;
use Pod::Usage;
use File::Spec::Functions;
use File::Path qw ( make_path );
use Fcntl qw ( O_WRONLY O_CREAT O_TRUNC O_EXCL );
use strict;
use warnings;

# Parse command-line options
my $verbosity = 1;
my $distroot = "/var/lib/extsys/dist";
my $distname = "External Packages";
my $force = 0;
Getopt::Long::Configure ( 'bundling', 'auto_abbrev' );
GetOptions (
  'verbose|v+' => sub { $verbosity++; },
  'quiet|q+' => sub { $verbosity--; },
  'root|r=s' => sub { shift; $distroot = shift; },
  'name|n=s' => sub { shift; $distname = shift; },
  'force|f' => sub { $force = 1; },
  'help|h|?' => sub { pod2usage(1); },
) or pod2usage ( "Could not parse command-line options" );
die "No distribution version specified\n" unless @ARGV == 1;
my $distversion = shift;
my $distdir = catdir ( $distroot, $distversion );

print "Creating distribution tree \"".$distname."\" in ".$distdir."\n"
    if $verbosity >= 1;

make_path ( catdir ( $distdir, "SRPMS", "contrib", "release" ),
	    catdir ( $distdir, "i586", "media", "media_info" ),
	    catdir ( $distdir, "i586", "media", "contrib", "release" ),
	    catdir ( $distdir, "x86_64", "media", "media_info" ),
	    catdir ( $distdir, "x86_64", "media", "contrib", "release" ) );

my $media_cfg = {};
$media_cfg->{x86_64} = <<__END_MEDIA_CFG__;
[media_info]
version=$distversion
mediacfg_version=2
arch=x86_64
product=$distname packages for Mandriva
xml-info=1

[contrib/release]
name=$distname
srpms=../../SRPMS/contrib/release

[debug_contrib/release]
name=$distname debug
srpms=../../SRPMS/contrib/release
noauto=1
debug_for=contrib/release

[../../i586/media/contrib/release]
name=$distname (32-bit)
arch=i586

[../../SRPMS/contrib/release]
name=$distname sources
rpms=contrib/release debug_contrib/release
noauto=1
__END_MEDIA_CFG__


$media_cfg->{i586} = <<__END_MEDIA_CFG__;
[media_info]
version=$distversion
mediacfg_version=2
arch=i586
product=$distname packages for Mandriva
xml-info=1

[contrib/release]
name=$distname
srpms=../../SRPMS/contrib/release

[debug_contrib/release]
name=$distname debug
srpms=../../SRPMS/contrib/release
noauto=1
debug_for=contrib/release

[../../SRPMS/contrib/release]
name=$distname sources
rpms=contrib/release debug_contrib/release
noauto=1
__END_MEDIA_CFG__


my $open_mode = ( $force ?
		  ( O_WRONLY | O_CREAT | O_TRUNC ) :
		  ( O_WRONLY | O_CREAT | O_EXCL ) );
foreach my $arch ( keys %$media_cfg ) {
  my $filename =
      catfile ( $distdir, $arch, "media", "media_info", "media.cfg" );
  sysopen my $fh, $filename, $open_mode
      or die "Could not open ".$filename.": $!\n";
  syswrite $fh, $media_cfg->{$arch};
  close $fh;
}
